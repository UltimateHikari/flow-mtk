import stack;
import arexp;

/**
 * polymorphic rpn
 */
export {
    RpnE<?> ::= RpnOp<?>, RpnArg<?>;
    RpnArg : (val: ?);
    RpnOp : (op: string, foo: (?, ?) -> ?);

    ae2rpni(a: ArExp) -> [RpnE<int>];
    rpni2rpnae(r : [RpnE<int>]) -> [RpnE<ArExp>];

    rpni2s(r: [RpnE<int>]) -> string;
    evalRpn(r : [RpnE<?>]) -> Maybe<?>;

}
ae2rpni(a: ArExp) -> [RpnE<int>]{
    switch(a){
        //e + e -> e e +
        ArSum(l,r): concat3(ae2rpni(l), ae2rpni(r), [RpnOp("+", \x, y -> x + y)]);
        ArProd(l,r): concat3(ae2rpni(l), ae2rpni(r), [RpnOp("*", \x, y -> x * y)]);
        ArInt(v): ([RpnArg(v)]);
    }
}

rpni2rpnae(r : [RpnE<int>]) -> [RpnE<ArExp>]{
    map(r, \ e -> switch (e : RpnE<int>) {
        RpnArg(v) : RpnArg(ArInt(v));
        RpnOp(op, foo) : 
            if(op == "+") { 
                RpnOp(op, \a, b -> ArSum(a,b)) 
            } else { 
                RpnOp(op, \a, b -> ArProd(a,b)) 
            };
    })
}

rpni2s(r: [RpnE<int>]) -> string{
    ltrim2(
        fold(r, "", 
            \acc, elem -> strGlue(
                [acc,   
                switch (elem : RpnE) {
                    RpnArg(val) : i2s(val);
                    RpnOp(op, foo) : op; 
                }],
                " ")
        ),
        " ")
}

evalRpn(r : [RpnE<?>]) -> Maybe<?>{
    popStack(
        fold(r, makeStack(), \s, e -> 
        switch (e: RpnE) {
            RpnArg(v) : pushStack(s, v);
            RpnOp(op, foo) : {
                /**
                 * on incorrect rpn
                 * just ruin everything, 
                 * func will return None()
                 */
                pop1 = popStack(s);
                pop2 = popStack(pop1.stack);
                eitherFn2(
                    pop1.value, 
                    pop2.value, 
                    \a,b -> pushStack(pop2.stack, foo(b,a)),
                    \a -> makeStack(),
                    makeStack
                    );
            }
        })
    ).value;
}

