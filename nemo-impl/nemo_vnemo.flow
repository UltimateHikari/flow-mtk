import ds/tree;
import ds/array;
import string;
import maybe;

export{
    vProg : (h: [vDecl], b: vBody);
    vDecl: (v: vVar, t: vType);
    vBody : (m: [vLabel]);
    vLabel: (id: int, i: vInstr, l: [int]);

    vInstr ::= vAssign, vIf, vPrint;
    vAssign : (v: vVar, e: vExp);
    vIf : (c: vExp);
    vPrint : (e: vExp);

    vVal ::= vInt, vArr;
    vInt: (v: int);
    vArr: (a: [vVal]);

    vVar(n: string);
    vType ::= vBType, vArrType;
    vBType : (t: string);
    vArrType : (t: vType);

    vExp ::= vVal, vVar, vSum, vSub, vProd, vDiv, vSet, vGet, vLen, vNot;
    vSum : (l: vExp, r: vExp);
    vProd : (l: vExp, r: vExp);
    vSub : (l: vExp, r: vExp);
    vDiv : (l: vExp, r: vExp);
    vSet : (a: vVar, i: vInt, v: vExp);
    vGet : (a: vVar, i: vInt);
    vLen : (a: vVar);
    vNot : (e: vExp);

    vExecute(p: vProg) -> void;
}

State:(labels: [int], vars: Tree<string, vVal>);

vExecute(p: vProg) -> void{
    vars = initDeclsTree(p.h);
    while(p.b, State([0], vars), 0);
}

while(b: vBody, state: State, testIterLimit:int) -> void{
    stoplabel = length(b.m);
    iterationState = fold(state.labels, State([], state.vars), 
        \acc, ind -> {
            newState = labelExecute(b.m[ind], acc.vars);
            State(concat(acc.labels, newState.labels), newState.vars);
            });
    //println(iterationState);
    filteredState = State(
        filtermap(iterationState.labels, \l -> if(l==stoplabel){ None() }else{ Some(l) }),
        iterationState.vars
        );
    if(length(filteredState.labels) > 0 && testIterLimit < 100){
        while(b, filteredState, testIterLimit + 1);
    } else {
        println("vNeMo Executor: done(" + i2s(testIterLimit) + ")"); //workaround
    }
}

notStopped(labels: [int], stoplabel: int) -> bool{
    filtered = filtermap(labels, \l -> if(l==stoplabel){None()}else{Some(l)});
    if(
        length(filtered) == 0
        ){
            false;
        // length(labels) == 0 || 
        // (length(labels) == 1 && labels[0] == stoplabel)
        // ){
        //     false;
    }else{
        true;
    }
}

labelExecute(lbl: vLabel, vars: Tree<string, vVal>) -> State {
    //println("executing: " + i2s(lbl.id));
    switch (lbl.i) {
        vAssign(v, e): {
            State(lbl.l, setTree(vars, v.n, expEval(e, vars)));
        }
        vIf(c): {
            if(expEval(c, vars) != vInt(0)){
                State(lbl.l, vars);
            } else {
                State([], vars);
            }
        }
        vPrint(e): {
            println(v2s(expEval(e, vars)));
            State(lbl.l, vars);
        }
    }
}

expEval(e: vExp, vars: Tree<string, vVal>) -> vVal{
    switch (e) {
        vInt(v): vInt(v);
        vArr(v): vArr(v);
        vVar(v): vLookup(vVar(v), vars);
        vSum(l, r)  : vInt(
            v2i(expEval(l, vars)) + 
            v2i(expEval(r, vars))
            );
        vProd(l, r) : vInt(
            v2i(expEval(l, vars)) * 
            v2i(expEval(r, vars))
            );
        vSub(l, r)  : vInt(
            v2i(expEval(l, vars)) - 
            v2i(expEval(r, vars))
            );
        vDiv(l, r)  : vInt(
            v2i(expEval(l, vars)) / 
            v2i(expEval(r, vars))
            );
        vSet(a, i, v) : vInt(0); //TODO move out of Exp
        vGet(a, ind) :
            valueGet(vLookup(a, vars), ind.v);
        vLen(a):
            vLength(vLookup(a, vars));
        vNot(a):
            {
                res = expEval(a, vars);
                if(res == vInt(0)){
                    vInt(1);
                }else{
                    vInt(0);
                }
            }
    }
}

v2i(val: vVal) -> int{
    switch (val) {
        vInt(v): v;
        vArr(a): {
            println("Runtime error: Arr in v2i");
            quit(-1);
            -1;
        };
    }
}

vLookup(var: vVar, vars: Tree<string, vVal>) -> vVal{
    switch (lookupTree(vars, var.n)) {
        Some(v): v;
        None(): 
        {
            println("Runtime error: variable not found");
            quit(-1);
            vInt(-1);
        }
    }
}

vLength(val: vVal) -> vInt{
    switch (val) {
        vArr(v): vInt(length(v));
        vInt(v): {
            println("Runtime error: len(Int)");
            quit(-1);
            vInt(-1);
        }
    }
}

valueGet(val: vVal, i: int) -> vVal{
    switch(val){
        vArr(v): {
            if(i < length(v)){
                v[i]; 
            } else { 
                println("Runtime error: OOB " + i2s(i) + " on " + v2s(val));
                quit(-1);
                vInt(-1);
            }
        };
        vInt(v): { 
            println("Runtime error: Int in valueGet");
            quit(-1);
            vInt(-1);
        };
    }
}

v2s(val: vVal) -> string{
    switch (val) {
        vArr(v): 
            "[" + 
            strGlue(
            map(v, \x -> v2s(x)),
            ", ") + 
            "]";
        vInt(v): i2s(v);
    }
}

initDeclsTree(h: [vDecl]) -> Tree<string, vVal> {
    fold(h, makeTree(), \acc, decl -> setTree(acc, decl.v.n, initType(decl.t)));
}

initType(t: vType) -> vVal{
    switch (t) {
        vArrType(v): vArr([initType(v)]);
        vBType(v): vInt(0);
    }
}

