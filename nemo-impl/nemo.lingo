prog = ws decl+:d annt:bc ws "{" ws body:b ws "}" ws annt:ac { Prog(:d, :b, :bc, :ac) };
decl = "VAR" ws var:v ws ":" ws type:t ws ";" ws   { Decl(:v, :t) };

val = int | arr;
int = digit+$v          { Int(s2i($v)) };
arr = "[" ws val*:a "]" { Arr(:a) };
entr = val ws;

var = char+$v   { Var($v)      };
digit = '0'-'9';
char = 'a'-'z' ;

type = primtype | arrtype;
primtype = "INT"                { bType("int") };
arrtype  = "[" ws type:t ws "]" { arrType(:t)  };

body = assign | test | seq | union | iter | print | while; 
assign = "(" ws var:v ws ":=" ws exp:e ws ")"                { nAssign(:v, :e)    };
test   = "(" ws exp:e ws "?"  ws ")"                         { nTest(:e)          };
seq    = "(" ws sentry+:e body:b ws ")"                      { buildSeq(:e, :b)   };
sentry = body:b ws ";" ws                                    { :b                 };
union  = "(" ws uentry+:e body:b ws ")"                      { buildUnion(:e, :b) };
uentry = body:b ws "U" ws                                    { :b                 };
iter   = "(" ws annt:a ws body:b ws "*" ws ")"               { nIter(:a, :b)      };
print  = "(" ws "PRINT" ws exp:e ws ")"                      { nPrint(:e)         };
while  = "(" ws annt:a ws "WHILE" ws exp:e ws "DO" ws body:b ws "DONE" ws ")" { buildWhile(:a, :e, :b) };

annt = "@" ws formula:f                                           { Annotation(:f)        };
formula = forall | exists | or | and | impl | neg | exp | repl | nop;
forall  = "(" ws "A" ws var:x ws formula:e ws ")"                 { Forall(:x, :e)  };
exists  = "(" ws "E" ws var:x ws formula:e ws ")"                 { Exists(:x, :e)  };
or      = "(" ws formula:l ws "|" ws formula:r ws ")"             { Or(:l, :r)      };
and     = "(" ws formula:l ws "&" ws formula:r ws ")"             { And(:l, :r)     };
impl    = "(" ws formula:l ws "->" ws formula:r ws ")"            { Impl(:l, :r)    };
neg     = "(" ws "NOT" ws formula:e ws ")"                        { Neg(:e)         };
repl    = "(" ws formula:f ws "R" ws var:x ws "W" ws exp:e ws ")" { Repl(:f, :x, :e)};
nop     = "(" ws "NOP" ws ")"                                     { Nop()           };

exp = var | val | sum | sub | prod | div | set | get | len | not | less | more | eless | emore | eq;
sum    = "(" ws exp:l ws "+" ws exp:r ws ")"            { Sum(:l, :r)      };
sub    = "(" ws exp:l ws "-" ws exp:r ws ")"            { Sub(:l, :r)      };
prod   = "(" ws exp:l ws "*" ws exp:r ws ")"            { Prod(:l, :r)     };
div    = "(" ws exp:l ws "/" ws exp:r ws ")"            { Div(:l, :r)      };
set    = "(" ws "SET" ws var:a ws int:i ws exp:v ws ")" { aSet(:a, :i, :v) };
get    = "(" ws "GET" ws var:a ws int:i ws ")"          { aGet(:a, :i)     };
len    = "(" ws "LEN" ws var:a ws ")"                   { aLen(:a)         };
not    = "(" ws "!" ws exp:e ws ")"                     { Not(:e)          };
less   = "(" ws exp:l ws "<" ws exp:r ws ")"            { Less(:l, :r)     };
more   = "(" ws exp:l ws ">" ws exp:r ws ")"            { Less(:r, :l)     };
eless   = "(" ws exp:l ws "<=" ws exp:r ws ")"          { Not(Less(:r, :l))};
emore   = "(" ws exp:l ws ">=" ws exp:r ws ")"          { Not(Less(:l, :r))};
eq      = "(" ws exp:l ws "==" ws exp:r ws ")" { And(Not(Less(:r, :l)), Not(Less(:l, :r))) };

ws = s*;
s = " " | "\t" | "\n";
comma = ws | ", ";